//created on: Aug 22, 2012
package com.rhc.brms.ref.rules

//list any import classes here.

import com.rhc.mortgage.domain.Application;
import com.rhc.mortgage.domain.Customer;
import com.rhc.mortgage.domain.Mortgage;
import java.math.BigDecimal;


rule "Customer is a Minor"
    agenda-group "eligible"
    when
        $customer : Customer
		(
			valid == true,
			$id		:	id,
			$age	: 	age < 18
        )
         
        $application : Application
		(
        		valid == true,	
        		reviewed != true,
        		customerId == $id,
        		$appId	:	applicationId
        )
        
    then
        
        modify( $application ){
        	setReviewed( true ),
        	setApproved( false )
        };
        
        System.out.println( "Application " + $appId + " denied due to applicant being a minor" );
end

rule "Poor Credit Score"
	agenda-group "approve"
    when
        
        $customer :	Customer	
        (
			valid == true,
			$id		:	id,
			$score	:	creditScore < 650
		) 
		
        $application : Application
        (
			valid == true,
			reviewed != true,
			customerId == $id,
			$appId	:	applicationId
        )
        
    then
        
        modify( $application ){
        	setReviewed( true ),
        	setApproved( false )
        };
        
        System.out.println( "Application " + $appId + " denied due to poor credit score" );

end

rule "Auto-Approve Under 10k"
	agenda-group "approve"
    
    when
        
        $customer : Customer	
        (
			valid == true,
			$id		:	id,
			$score	:	creditScore > 650
		)
		 
        $application : Application
        (
			valid == true,
			reviewed != true,
			customerId == $id,
			$amount : 	amount < 10000,
			$appId	:	applicationId
        )
        
    then

        modify( $application ){
        	setReviewed( true ),
        	setApproved( true )
        };
        
        insert( new Mortgage( $id, $appId, $amount ) );

        System.out.println( "Application " + $appId + " approved for amount " + $amount );

end

rule "Limit mortgage at 10k"
	agenda-group "approve"
    when
        
        $customer	:	Customer	
        (
			valid == true,
			$id		:	id,
			$score	:	creditScore > 650
        )
         
        $application : Application
        (
			valid == true,
			reviewed != true,
			customerId == $id,
			$amount : 	amount > 10000,
			$appId	:	applicationId
        )
        
    then
        
        modify( $application ){
        	setReviewed( true ),
        	setApproved( true )
        }
        
        insert( new Mortgage( $id, $appId, new BigDecimal( 10000 ) ) );
        
        System.out.println( "Application " + $appId + " approved for max of $10,000" );

end